/*
  The humble beginnings of a booking system.

  Once Cixl is installed, tests may be run by executing
  "load '../examples/bookings.cx'" followed by "run-tests".

  https://github.com/basic-gongfu/cixl#getting-started
*/

include: 'qdb.cx';

(

let: resources  'resource.db' ([`id]) new-db-table;
let: bookings   'booking.db'  ([`id]) new-db-table;     

let: next-booking-id 1 ref;

/*
  Resources support tracking availability in time.
*/

rec: Resource()
  id       Sym
  quantity Int
  calendar Vect;

/*
  Capacities keep track of total and used quantity for a specific resource and
  interval. They are used as building blocks for calendars.
*/

rec: Capacity()
  start end  Time
  total used Int;

func: new-capacity(start end Time total used Int) (Capacity)
  let: c Capacity new;
  $c `start $start put
  $c `end   $end   put
  $c `total $total put
  $c `used  $used  put
  $c;

/*
  Resources start out with one continous segment of full capacity in the calendar.
*/

func: new-resource(id Sym q Int) (Resource)
  let: r Resource new;
  let: c [#min-time #max-time $q 0 new-capacity];
  $r `id       $id put
  $r `quantity $q  put
  $r `calendar $c  put
  $resources $r upsert
  $r;

/*
  Capacities are split as needed during update.
*/

func: update-capacity(in Capacity start end Time total used Int out Vect) ()
  $in `start get $start < {(
    let: head $in %% ~ _;
    $head `end   $start put
    $in   `start $start put
    $out $head push
  )} if

  $out $in push

  $in `end get $end > {(
    let: tail $in %% ~ _;
    $tail `start $end put
    $in   `end   $end put
    $out $tail push
  )} if

  let: t $in `total get $total +;
  let: u $in `used get $used +;
  $u $t > {'Overbook' fail} if
  $in `total $t put
  $in `used  $u put;

func: intersects(in Capacity start end Time) (Bool)
  {$in `start get $end <} {$in `end get $start >} and;

/*
  The entire calendar is scanned for each update, records are cloned to avoid
  inconsistent calendars on failure.
*/

func: update-calendar(in Vect start end Time total used Int) (Vect)
  let: out [];

  $in {
    % $start $end intersects {
      %% $start $end $total $used $out update-capacity
    } if _
  } for
  
  $out;

func: update-resource(r Resource start end Time total used Int) ()
  let: c $r `calendar get;
  $r `calendar ($c $start $end $total $used update-calendar) put;

/*
  Bookings represent used quantity for a specific resource and interval.
*/

rec: Booking()
  id        Int   
  resource  Resource
  start end Time
  quantity  Int;

func: next-booking-id() (Int)
  let: id $next-booking-id get-ref;
  $next-booking-id $id ++ put-ref 
  $id;

func: new-booking(r Resource start end Time q Int) (Booking)
  $start $end < {'Invalid booking length' fail} else
  let: b Booking new;
  $b `id       next-booking-id put
  $b `resource $r              put
  $b `start    $start          put
  $b `end      $end            put
  $b `quantity $q              put
  $r $start $end 0 $q update-resource
  $bookings $b upsert
  $b;

/*
  This should fail with an overbook error since it tries to book 11 out of 10 for
  the next hour.
*/

func: run-tests() ()
  let: r Sym new 10 new-resource;
  $r today (today 1 days +) 5 new-booking _
  $r now   (now 1h +)       6 new-booking _;

)