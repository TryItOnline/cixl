, is-let `min-time if {} {
  let: min-time [] time;
  let: max-time [9999 11 30] time;
}

/*
  Resources support tracking availability in time.
*/

rec: Resource()
  quantity Int
  calendar Vect;

/*
  Capacities keep track of total and used quantity for a specific resource and
  interval. They are used as building blocks for resource calendars.
*/

rec: Capacity()
  start end  Time
  total used Int;

func: new-capacity(start end Time total used Int)
  let: c Capacity new;
  repeat: ($c put)
    `start    $start,
    `end      $end,
    `total    $total,
    `used     $used;
  $c;

/*
  Resources start out with one continous segment of full capacity in the calendar.
*/

func: new-resource(q Int)
  let: r Resource new;
  $r put `quantity $q
  $r put `calendar [new-capacity $min-time $max-time $q 0]
  $r;

/*
  Capacities are split into several segments when updated.
*/

func: update-capacity(in Capacity start end Time total used Int out Vect)
  $in get `start < $start if {(
    let: head $in %%;
    $head put `end $start
    $in put `start $start
    $out push $head
  )} {}

  $out push $in

  $in get `end > $end if {(
    let: tail $in %%;
    $tail put `start $end
    $in put `end $end
    $out push $tail
  )} {}

  let: t get $in `total + $total;
  let: u get $in `used + $used;
  if $u > $t {fail 'Overbook'} { }
  $in put `total $t
  $in put `used $u;

func: update-calendar(in Vect start end Time total used Int)
  let: out [];
  $in for {update-capacity $start $end $total $used $out}
  $out;

func: update-resource(r Resource start end Time total used Int)
  let: c $r get `calendar;
  $r put `calendar,
  update-calendar $c $start $end $total $used;

/*
  Bookings represent used quantity for a specific resource and interval.
*/

rec: Booking()
  resource  Resource
  start end Time
  quantity  Int;

func: new-booking(r Resource start end Time q Int)
  $end <= $start if {fail 'Invalid booking length'} {}
  let: b Booking new;
  repeat: ($b put)
    `resource $r,
    `start    $start,
    `end      $end,
    `quantity $q;
  update-resource $r $start $end 0 $q
  $b;

/*
  Bookings represent used quantity for a specific resource and interval.
*/

func: run-tests()
  let: r new-resource 10;
  new-booking $r today today ,+ 1 days 5 _
  new-booking $r now now ,+ 1h 6;