#!/usr/local/bin/cixl

/*
  Scans a range of TCP/IP ports for a host and prints status to stdout.
  
  Execute ```cixl cixl/examples/tcpscan.cx 127.0.0.1 1 100``` to scan
  ports 1-100 on localhost.
  
  https://github.com/basic-gongfu/cixl#getting-started
*/

use:
  (cx/abc   Int)
  (cx/cond  ? if)
  (cx/func  func:)
  (cx/io    Poll close delete on-read say wait)
  (cx/iter  for while)
  (cx/net   tcp-connect)
  (cx/math  + -)
  (cx/stack ~ _)
  (cx/str   int)
  (cx/type  new)
  (cx/var   let:);

let: (max-port min-port) int ~ int;
let: host;
let: poll Poll new;

['Scanning ports ' $min-port @- $max-port ' on ' $host @@n] say

func: connect(port Int)()
  let: c $host $port tcp-connect;

  $c? {
    $poll $c {
      $port say
      $poll $c delete
      $c close      
    } on-read
  } if;

$max-port $min-port - {
  $min-port + connect
  $poll 0 wait _
} for

{$poll 100 wait} while

'Done!' say