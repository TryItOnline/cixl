/*
  A Mandelbrot fractal zoom implemented using ANSI graphics.
  Depends on libcairo and the cximage plugin.
  The animation may be started by executing "cixl examples/mandelbrot.cx".

  https://github.com/basic-gongfu/cixl#getting-started
*/

link:
  'libcairo'
  'libcximage';

init: 'image';

use: cx cx/image;

define: max 4.0;
define: max-iter 570;
define: black 0 0 0 rgb;

let: (max-x max-y) screen-size;
let: max-cx $max-x 3.0 / 2.0 *;
let: max-cy $max-y 2.0 /;
let: frame $max-x $max-y new-frame;
let: is-black Stack<Int> new;
let: zoom 0 ref;

func: render()()
  $frame #black fill-color
  $is-black % clear $max-x 0 repeat
  
  $max-y 2 / ++ {
    let: y;

    $max-x {
      let: x;

      $is-black $x get 1 > {
        $frame $x $y #black put-xy
      } {
        let: (zx zy) 0.0 ref %%;
        let: cx $x $max-cx - $zoom deref /;
        let: cy $y $max-cy - $zoom deref /;
        let: i #max-iter ref;

        {
          let: nzx $zx deref ** $zy deref ** - $cx +;
	  $zy $zx deref *2 $zy deref * $cy + set
	  $zx $nzx set
          $i &-- set-call	
          $nzx ** $zy deref ** + #max < $i deref and
        } while

        let: c $i deref % -7 bsh bor 256 mod;
	
        $frame $x $y $c {
          $x 256 mod $y 256 mod
        } {
          $is-black $x &++ put-call
          0 0
        } if-else $c rgb put-xy
      } if-else
    } for
  } for

  #out 1 1 xy move-to
  $frame 0 $max-y 2 / % vfold
  $frame #out print;

#out hide-cursor
raw-mode

let: poll Poll new;
let: is-done #f ref;

$poll #in {
  #in read-char _
  $is-done #t set
} on-read

{
  $zoom &++ set-call
  render
  $poll 0 wait _
  $is-done deref !
} while

#out reset-style
#out clear-screen
#out 1 1 xy move-to
#out show-cursor
normal-mode