#!/usr/local/bin/cixl

/*
  The seed of a simplified IRC.

  https://github.com/basic-gongfu/cixl#getting-started
*/

use:
  (cx/abc A Opt Stack Str Sym #nil #t is-nil)
  (cx/buf Buf push write-bytes)
  (cx/cond = else if if-else switch:)
  (cx/error fail)
  (cx/func func:)
  (cx/guid Guid)
  (cx/io RFile WFile #in #out ask include: lines print say)
  (cx/io/poll Poll delete no-write on-read on-write wait)
  (cx/iter Iter for next while)
  (cx/net TCPServer accept listen)
  (cx/rec ? rec: get put)
  (cx/str join split words)
  (cx/sym sym)
  (cx/sys home-dir make-dir)
  (cx/stack _ push)
  (cx/table Table put)
  (cx/type new)
  (cx/var let:);

include: 'qdb.cx';
use: (qdb commit len new-db-table upsert);

func: get-path(n Str)(_ Str)
  [home-dir '.ircx' $n] @/ join;

'' get-path make-dir

let: users 'users.db' get-path [`name] new-db-table;
let: threads 'threads.db' get-path [`id] new-db-table;
let: root Table new;
let: poll Poll new;

rec: Thread ()
  id name  Sym
  info     Str
  parent   Thread
  children Table;

func: save(t Thread)()
  let: rec Table new;
  $rec `id $t `id get put
  $rec `name $t `name get put
  $rec `info $t `info get put
  
  let: pt $t `parent get;
  $pt {$rec `parent $pt `id get put} if
  $threads $rec upsert;

func: add-thread(pt Opt t Thread)()
  let: pc $pt `children get;

  $pc {
    $pc $t `name get $t put
  } {
    let: pc Table new;
    $pt `children $pc put
    $pc $t `name get $t put    
  } if-else;

func: add-thread(#nil t Thread)()
  $root $t `name get $t put;

rec: User ()
  name   Sym
  thread Thread;

func: save(u User)()
  let: rec Table new;
  $rec `name $u `name get put
  let: t $u `thread get;
  $t {$rec `thread $t `id get put} if
  $users $rec upsert;

func: new-user (n Sym)(_ User)
  let: u User new;
  $u `name $n put
  $u save
  $u;

func: new-thread(u User n Sym)()
  let: t Thread new;
  let: id [];
  
  $t `name $n put
  $t `info '' put
  
  let: pt $u `thread get;
  
  $pt {
    $t `parent $pt put
    $pt $t add-thread
    $id $pt `id get push
  } {
    #nil $t add-thread
    $id '' push
  } if-else

  $id $n push
  $t `id $id @/ join sym put
  $t save;

func: join-thread(u User ns Str)()
  $ns 0 get @/ = {$u `thread #nil put} if
  
  $ns @/ split {
    let: n;
    let: ut $u `thread get;

    $n '..' = {
      $ut {'Thread not found: ..' fail} else
      $u `thread $ut `parent get put  
    } {
      $ut {
        let: tc $ut `children get;
        $tc {['Thread not found: ' $n] #nil join fail} else
    
        let: t $tc $n sym get;
        $t {['Thread not found: ' $n] #nil join fail} else

        $u `thread $t put
      } {
        let: t $root $n sym get;
        $t {['Thread not found: ' $n] #nil join fail} else      
        $u `thread $t put
      } if-else
    } if-else
  } for;

func: get-thread-id(u User)(_ Sym)
  let: t $u `thread get;
  $t {$t `id get} `/ if-else;

rec: Client ()
  user User
  in   Iter
  out  WFile
  buf  Buf;

func: push-out(c Client v A)()
  let: buf $c `buf get;

  $buf {
    let: out $c `out get;
    
    $poll $out {
      $out $buf write-bytes
      $buf {$poll $out no-write} else
    } on-write
  } else

  $buf $v push;

func: prompt(c Client)()
  let: u $c `user get;
  
  $u {
    $c $u `name get push-out
    $c @@s push-out
    $c $u get-thread-id push-out
    $c '> ' push-out
  } {
    $c 'login> ' push-out
  } if-else;

func: handle-in(c Client in Str)()
  $c `user get {
    $in {
      $in 0 get @: = {
        let: ins $in @@s split;
        let: cmd $ins next sym;

        switch:
          (($cmd `:join =)
            $c `user get $ins next join-thread)
          (#t
            ['Invalid cmd: ' $cmd] #nil join fail);
      } {
        'post' say
      } if-else
    } if
  } {
    $c `user $in sym new-user put
  } if-else;

func: new-client(in RFile out WFile u Opt)()
  let: c Client new;
  let: ils $in lines;

  $c `in $ils put
  $c `out $out put
  $c `buf Buf new put
  $u {$c `user $u put} if
  
  $poll $in {
    let: l $ils next;
  
    $l is-nil {
      $c $l handle-in
      $c prompt
    } else
  } on-read

  $c prompt;

['ir.cx v0.1' @@n] say

$users len {
  ['Database Setup' @@n] say
  'Operator Name: ' ask
  sym new-user
  [@@n 'Done!' @@n] say
} else

let: u `me new-user;
$u `foo new-thread
$u 'foo' join-thread
$u `bar new-thread
$u 'bar' join-thread
commit

#in #out $u new-client

let: server #nil 7707 3 listen;

$poll $server {
      let: c $server accept;
      $c {$c $c #nil new-client} if
} on-read

{$poll -1 wait} while