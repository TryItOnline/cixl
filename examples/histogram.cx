#!/usr/local/bin/cixl

/*
  Produces a word histogram from stdin.
  
  Execute ```cixl cixl/examples/histogram.cx < file1 ... fileN``` to get a histogram
  of words in file1-N.
  
  https://github.com/basic-gongfu/cixl#getting-started
*/

use:
  (cx/cond <=>)
  (cx/iter for)
  (cx/io #in say)
  (cx/io/term say)
  (cx/math ++)
  (cx/pair x y)
  (cx/stack | ~ % sort stack)
  (cx/str lower words)
  (cx/table Table put-else)
  (cx/type new)
  (cx/var let:);

/*
  Split stdin into lowercase words and count them using a table.
*/

| let: ws Table new;

#in words { % lower $ws ~ 1 &++ put-else } for

/*
  Convert table to vector of pairs and sort by descending value, pairs are already
  sorted alphabetically since the table is sorted by key.
*/

$ws stack % {y ~ y <=>} sort

/*
  Print results to stdout.
*/

{
  % x ~ y let: (x y);
  [$x @@s $y] say
} for