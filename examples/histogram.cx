#!/usr/local/bin/cixl

/*
  Produces a word histogram from stdin.
  
  Execute ```cixl cixl/examples/histogram.cx < file1 ... fileN``` to get a histogram
  of words in file1-N.
  
  https://github.com/basic-gongfu/cixl#getting-started
*/

use:
  (cx/abc is-nil)
  (cx/cond <=> if-else)
  (cx/iter for)
  (cx/io #in)
  (cx/io/term say)
  (cx/math ++)
  (cx/pair x y unzip)
  (cx/stack | _ ~ % sort stack)
  (cx/str lower words)
  (cx/table Table put-call)
  (cx/type new)
  (cx/var let:);

/*
  Split stdin into lowercase words and count them using a table.
*/

| let: ws Table new;

#in words {% lower $ws ~ {% is-nil {_ 1} &++ if-else} put-call} for

/*
  Convert table to vector of pairs and sort by descending value, pairs are already
  sorted alphabetically since the table is sorted by key.
*/

$ws stack % {y ~ y <=>} sort

/*
  Print results to stdout.
*/

{
  let: (x y) unzip;
  [$x @@s $y] say
} for