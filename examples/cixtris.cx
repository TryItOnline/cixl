/*
  A console Tetris clone.
  Press ESC to exit gracefully.
  
  https://github.com/basic-gongfu/cixl#getting-started
*/

include: 'ansi.cx';

use:
  (cx/abc #t #f #nil A Bool Char Int Opt is-nil)
  (cx/cond ! ? = == < > <= and else if if-else min max or)
  (cx/const define:)
  (cx/error throw)
  (cx/func func:)
  (cx/io #in #out flush read-char print unblock)
  (cx/io/term
    Color
    #clear-row #clear-screen #hide-cursor #key-esc #reset-style #restore-pos
    #reverse-colors #save-pos #show-cursor
    hline move-down move-left move-right move-to new-color normal-mode raw-mode
    screen-size set-bg vline)
  (cx/io/poll Poll new on-read wait)
  (cx/iter find-if for map times while)
  (cx/math + - * ++ -- mod rand)
  (cx/rec ? print rec: get put put-call)
  (cx/ref Ref deref new ref set set-call)
  (cx/stack Stack _ ~ % %% fill get len put splat stack)
  (cx/str len)
  (cx/var let:);

/*
   These are the shapes we'll be using to represent blocks visually.
*/ 

define: shapes [
  [['**'
    '**']]

  [['****']

   ['*'
    '*'
    '*'
    '*']]

  [['  *'
    '***']

   ['*'
    '*'
    '**']

   ['***'
    '*']

   ['**'
    ' *'
    ' *']]

  [['*'
    '***']

   ['**'
    '*'
    '*']

   ['***'
    '  *']

   [' *'
    ' *'
    '**']]

  [[' *'
    '***']
    
   ['*'
    '**'
    '*']
    
   ['***'
    ' *']

   [' *'
    '**'
    ' *']]

  [[' **'
    '**']

   ['*'
    '**'
    ' *']]

  [['**'
    ' **']

   [' *'
    '**'
    '*']]
];

/*
   And these are their respective colors, the same color is always used for a
   specific shape.
*/

define: colors [
  [255 255 0]
  [0 255 255]
  [255 128 0]
  [0 0 255]
  [255 0 255]
  [0 255 0]
  [255 0 0]
] {splat new-color} map stack;

define: key-rotate @i;
define: key-left   @j;
define: key-right  @l;
define: key-drop   @k;
define: key-fast   @+;
define: key-slow   @-;
define: key-quit   #key-esc;

define: speed-delays [125 100 75 50 25];
define: max-speed #speed-delays len --;
let: speed 0 ref;

let: max-x 30;
let: max-y screen-size ~ _;

let: tiles [] % $max-x $max-y * #nil fill;
let: block Ref new;
let: next-block Ref new;

let: key-delay 0 ref;
let: is-done #f ref;
let: is-dropping #f ref;

/*
   Each block keeps track of its shapes, shape index, color and position.
*/

rec: Block()
  shapes Stack
  i      Int
  color  Color
  x y    Int;

func: shape-width(s Stack)(_ Int)
  0 $s {len max} for;

func: shape-height(s Stack)(_ Int)
  $s len;

/*
   New blocks get random shapes, shape indexes and x positions.
*/

func: new-block()(b Block)
  let: b Block new;
  let: si #shapes len rand;
  let: ss #shapes $si get;
  let: i $ss len rand;
  let: s $ss $i get;
  let: c #colors $si get;
  let: x $max-x rand ++ $max-x $s shape-width - min;
  
  $b `shapes $ss put
  $b `i      $i  put
  $b `color  $c  put
  $b `x      $x  put
  $b `y      1   put;

/*
   Returns the current shape for the specified block.
*/

func: get-shape(b Block)(_ Stack)
  $b % `shapes get ~ `i get get;

/*
   Since we'll be printing a lot, we reserve the letter 'p' for that purpose.
*/

func: p(_ A)()
  #out ~ print;

/*
   Prints or erases current shape of block at current position depending on specified
   color.
*/

func: print-shape(b Block c Opt)()
  #save-pos p
  $c {$c set-bg p} {#reset-style p} if-else
  
  $b get-shape {
    let: l;
    
    $l {
      @* = {
        @@s p
      } {
        1 move-right p
      } if-else
    } for

    1 move-down p
    $l len move-left p
  } for
  
  $c {#reset-style p} if
  #restore-pos p;

/*
   Prints bottom marker for block.
*/

func: print-marker(b Block)()
  #save-pos p
  $b `x get $max-y move-to p
  @@s $s shape-width hline p
  #restore-pos p;

/*
   Updates tiles for block starting at specified offset.
*/

func: update-tiles(b Block offs Int v Opt)()
  let: s $b get-shape;
  let: o $offs ref;
  
  $s {
    let: l;
    
    $l {
      @* = {$tiles $o deref $v put} if
      $o &++ set-call
    } for

    $max-x $l len - $o &+ set-call
  } for;

func: get-offs(x y Int)(_ Int)
  $y -- $max-x * $x -- +;
  
func: print-block(b Block)()
  $b $b `color get print-shape
  $b print-marker
  $b % % `x get ~ `y get get-offs $b update-tiles;

func: erase-block(b Block)()
  $b #nil print-shape
  $b % % `x get ~ `y get get-offs #nil update-tiles;

/*
   Returns #t if all tiles intersecting block starting from specified position are
   free.
*/

func: check-tiles(b Block s Stack x y Int)(_ Bool)
  let: o $x $y get-offs ref;

  $s {
    let: l;
    let: so $o deref;
    
    $l {
      @* = {
        let: bb $tiles $o deref get;
	$bb {$bb $b == !} and
      } #f if-else

      $o &++ set-call
    } find-if
    
    $o $so $max-x + set
  } find-if !;

func: next-shape(b Block)(_ Stack)
  let: ss $b `shapes get;
  $ss $b `i get ++ $ss len mod get;

func: rotate-block(b Block)()
  $b `i {++ $b `shapes get len mod} put-call;

func: refresh-speed()()
  #save-pos p
  $max-x 2 + 3 move-to p
  #reset-style p
  #out $speed deref ++ print  
  #restore-pos p;

func: refresh-next-block(b Block)()
  #save-pos p
  $max-x 2 + 6 move-to p
  #reset-style p

  $next-block {
    % {#nil print-shape} &_ if-else
    $b % `color get print-shape
    $b
  } set-call
  
  #restore-pos p;

func: handle-key(#key-rotate)()
   let: b $block deref;
   let: ns $b next-shape;
   let: ok $b $ns $b % `x get ~ `y get check-tiles;

   $ok {$b `x get $ns shape-width + $max-x <=} and {
     $b % erase-block rotate-block
   } if;
   
func: handle-key(#key-left)()
  let: b $block deref;
  
  $b `x {
    let: x;
	
    $x 1 > {
      let: nx $x --;
      
      $b $b get-shape $nx $b `y get check-tiles {
        $b erase-block
        $nx
      } $x if-else
    } $x if-else
  } put-call;

func: handle-key(#key-right)()
  let: b $block deref;
  let: s $b get-shape;
  
  $b `x {
    let: x;
	
    $x $max-x $s shape-width - < {
      let: nx $x ++;
      
      $b $b get-shape $nx $b `y get check-tiles {
        $b erase-block
        $nx
      } $x if-else
    } $x if-else
  } put-call;

func: handle-key(#key-drop)()
  $is-dropping #t set;

func: handle-key(#key-fast)()
  $speed {
    let: s;
    $s #max-speed < {$s ++} $s if-else
  } set-call

  refresh-speed;

func: handle-key(#key-slow)()
  $speed {
    let: s;
    $s {$s --} $s if-else
  } set-call

  refresh-speed;

func: handle-key(#key-quit)()
  $is-done #t set;

func: handle-key(_ Char)()
  _;

/*
   Initialize event loop and input callback.
*/

let: poll Poll new;

#in unblock

$poll #in {
  #in read-char handle-key
  1 $key-delay &+ set-call
} on-read

/*
   Initialize screen.
*/

raw-mode
#clear-screen p
#hide-cursor p

$max-x 1 move-to p
#reverse-colors p
@@s $max-y vline p
#reset-style p

$max-x 2 + 2 move-to p
'Speed:' p
refresh-speed

$max-x 2 + 5 move-to p
'Next:' p
$block new-block set
new-block refresh-next-block

func: k(k Char)(_ Stack)
  [#reverse-colors $k #reset-style];

$max-x 2 + $max-y 3 - move-to p
['Use ' #key-left k ' and ' #key-right k ' to move block horizontally,'] p

$max-x 2 + $max-y 2 - move-to p
[#key-rotate k ' rotates and ' #key-drop k ' drops.'] p

$max-x 2 + $max-y move-to p
[#key-fast k ' and ' #key-slow k ' may be used to change speed.'] p

/*
   Main loop
*/

{
  let: b $block deref;

  $key-delay deref {
    $key-delay &-- set-call
    $b `x get $b `y get move-to p
    $b print-block
  } {
    let: s $b get-shape;
    let: ok $b $s $b % `x get ~ `y get ++ check-tiles;
    $ok ! {$b `y get 1 =} and {$is-done #t set} if
    
    $ok {$b `y get $s shape-height + $max-y <} and {
      $b `x get $b `y get move-to p
      $b erase-block
      $b `y &++ put-call
      1 move-down p
      $b print-block
    } {
      let: bb $next-block deref;
      new-block refresh-next-block
      $block $bb set
      $bb `x get $bb `y get move-to p
      $bb print-block
      $is-dropping #f set
    } if-else
    
    #out flush
  } if-else

  let: sd $is-dropping deref 1 {#speed-delays $speed deref get} if-else;
  $poll $sd wait _
  
  1 $max-y move-to p
  #reverse-colors p
  @@s $max-x hline p
  #reset-style p

  $is-done deref !
} while

/*
   Restore console
*/

#reset-style p
#clear-screen p
1 1 move-to p
#out flush
normal-mode
#show-cursor p
